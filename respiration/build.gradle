apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.0"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    bintrayRepo = 'Respiration'
    bintrayName = 'Respiration'

    publishedGroupId = 'com.link184'
    libraryName = 'Respiration'
    artifact = 'respiration'

    libraryDescription = 'Simple way to subscribe to firebase database by rxJava patterns.'

    siteUrl = 'https://github.com/Link184/Respiration'
    gitUrl = 'https://github.com/Link184/Respiration.git'

    libraryVersion = '0.5.0'

    developerId = 'link184'
    developerName = 'Eugeniu Tufar'
    developerEmail = 'eugeniu.tufar@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}


dependencies {
//    compile("com.link184:respiration-annotations:${libraryVersion}")
    releaseApi project(':respiration-annotations')
    debugApi project(':respiration-annotations')

    // Rx
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
    api 'io.reactivex.rxjava2:rxjava:2.1.6'

    // "Firebase"
    api 'com.google.firebase:firebase-core:11.6.0'
    api 'com.google.firebase:firebase-auth:11.6.0'
    api 'com.google.firebase:firebase-storage:11.6.0'
    api 'com.google.firebase:firebase-database:11.6.0'
    api 'com.google.android.gms:play-services-auth:11.6.0'
}

publishing {
    publications {
        releasePublication(MavenPublication) {
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion
            artifact file("${rootDir}/respiration/build/outputs/aar/respiration-release.aar")
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: '../bintrayv1.gradle'

