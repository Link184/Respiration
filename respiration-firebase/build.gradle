apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName rootProject.libraryVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    testImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    testImplementation 'org.mockito:mockito-core:2.19.0'

//    compile "com.link184:respiration-core:$rootProject.libraryVersion"
//    compile "com.link184:respiration-annotations:$rootProject.libraryVersion"
    api project(':respiration-core')
    api project(':respiration-annotations')

    // Rx
    api 'io.reactivex.rxjava2:rxandroid:2.0.2'
    api 'io.reactivex.rxjava2:rxjava:2.1.14'

    // "Firebase"
    api 'com.google.firebase:firebase-core:16.0.1'
    api 'com.google.firebase:firebase-auth:16.0.2'
    api 'com.google.firebase:firebase-database:16.0.1'
    api 'com.google.android.gms:play-services-auth:15.0.1'
}

ext {
    bintrayRepo = 'Respiration'
    bintrayName = 'Firebase'

    publishedGroupId = 'com.link184'
    libraryName = 'RespirationFirebase'
    artifact = 'respiration-firebase'
}

publishing {
    publications {
        releasePublication(MavenPublication) {
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion
            artifact file("${rootDir}/respiration-firebase/build/outputs/aar/respiration-firebase-release.aar")
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

apply from: '../installv1.gradle'
apply from: '../bintrayv1.gradle'

